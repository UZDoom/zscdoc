class ZF_ButtonAnimator : ZF_Animator abstract {
	abstract void stateChange(ZF_ButtonBase elem, ZF_ButtonState old, ZF_ButtonState new, Object storage);
	abstract void textChange(ZF_ButtonBase elem, String old, String new, Object storage);
	abstract void textScaleChange(ZF_ButtonBase elem, double old, double new, Object storage);
	abstract void textColorChange(ZF_ButtonBase elem, int old, int new, Object storage);
	abstract void fontChange(ZF_ButtonBase elem, Font old, Font new, Object storage);
	abstract void alignmentChange(ZF_ButtonBase elem, ZF_AlignType old, ZF_AlignType new, Object storage);

	abstract void initialSetupDone(
		ZF_ButtonBase elem,
		ZF_AABB box,
		String text,
		double textScale,
		int textColor,
		Font font,
		ZF_AlignType alignment,
		ZF_ButtonState state,
		Object storage
	);
}

class ZF_BasicButtonAnimatorStorage {
	ZF_BoxImage images[7];
	ZF_Label label;
}

class ZF_BasicButtonAnimator : ZF_ButtonAnimator {
	private ZF_BoxTextures textures[7];

	static ZF_BasicButtonAnimator create(
		ZF_BoxTextures offNothing,
		ZF_BoxTextures offHover,
		ZF_BoxTextures offClick,
		ZF_BoxTextures onNothing,
		ZF_BoxTextures onHover,
		ZF_BoxTextures onClick,
		ZF_BoxTextures disabled
	) {
		let ret = new("ZF_BasicButtonAnimator");
		ret.textures[0] = offNothing;
		ret.textures[1] = offHover;
		ret.textures[2] = offClick;
		ret.textures[3] = onNothing;
		ret.textures[4] = onHover;
		ret.textures[5] = onClick;
		ret.textures[6] = disabled;
		return ret;
	}

	override ZF_AnimatorSetupResult setupOnElement(ZF_ElementContainer elem) {
		let button = ZF_ButtonBase(elem);
		let storage = new("ZF_BasicButtonAnimatorStorage");
		let result = ZF_AnimatorSetupResult.Create(storage);
		let pos = elem.getPos();
		let size = elem.getSize();
		for (int i = 0; i < 7; i++) {
			let img = textures[i] != NULL ? ZF_BoxImage.create((0, 0), size, textures[i]) : NULL;
			if (img != NULL) {
				img.hide();
				result.elements.push(img);
			}
			storage.images[i] = img;
		}
		let label = ZF_Label.create((0, 0), size);
		result.elements.push(label);
		storage.label = label;
		return result;
	}

	override void stateChange(ZF_ButtonBase elem, ZF_ButtonState old, ZF_ButtonState new, Object storage) {
		let storage = ZF_BasicButtonAnimatorStorage(storage);
		let oldIndex = old.getStateInt();
		let newIndex = new.getStateInt();
		if (storage.images[oldIndex] != NULL) storage.images[oldIndex].hide();
		if (storage.images[newIndex] != NULL) storage.images[newIndex].show();
	}
	override void textChange(ZF_ButtonBase elem, String old, String new, Object storage) {
		let storage = ZF_BasicButtonAnimatorStorage(storage);
		storage.label.setText(new);
	}
	override void textScaleChange(ZF_ButtonBase elem, double old, double new, Object storage) {
		let storage = ZF_BasicButtonAnimatorStorage(storage);
		storage.label.setTextScale(new);
	}
	override void textColorChange(ZF_ButtonBase elem, int old, int new, Object storage) {
		let storage = ZF_BasicButtonAnimatorStorage(storage);
		storage.label.setTextColor(new);
	}
	override void fontChange(ZF_ButtonBase elem, Font old, Font new, Object storage) {
		let storage = ZF_BasicButtonAnimatorStorage(storage);
		storage.label.setFont(new);
	}
	override void alignmentChange(ZF_ButtonBase elem, ZF_AlignType old, ZF_AlignType new, Object storage) {
		let storage = ZF_BasicButtonAnimatorStorage(storage);
		storage.label.setAlignment(new);
	}

	override void boxChange(ZF_ElementContainer elem, ZF_AABB old, ZF_AABB new, Object storage) {
		let storage = ZF_BasicButtonAnimatorStorage(storage);
		for (int i = 0; i < 7; i++) {
			if (storage.images[i] != NULL) storage.images[i].setBox((0, 0), new.size);
		}
		storage.label.setBox((0, 0), new.size);
	}

	override void initialSetupDone(
		ZF_ButtonBase elem,
		ZF_AABB box,
		String text,
		double textScale,
		int textColor,
		Font font,
		ZF_AlignType alignment,
		ZF_ButtonState state,
		Object storage
	) {
		let storage = ZF_BasicButtonAnimatorStorage(storage);
		for (int i = 0; i < 7; i++) {
			if (storage.images[i] != NULL) storage.images[i].setBox((0, 0), box.size);
		}
		storage.label.setBox((0, 0), box.size);
		let stateIndex = state.getStateInt();
		storage.images[stateIndex].show();
		storage.label.setText(text);
		storage.label.setTextScale(textScale);
		storage.label.setTextColor(textColor);
		storage.label.setFont(font);
		storage.label.setAlignment(alignment);
	}
}

enum ZF_ButtonMouseState {
	ZF_ButtonMouseState_Nothing,
	ZF_ButtonMouseState_Hover,
	ZF_ButtonMouseState_Clicked
}
enum ZF_ButtonLogicalState {
	ZF_ButtonLogicalState_Off,
	ZF_ButtonLogicalState_On
}
struct ZF_ButtonState {
	bool disabled;
	ZF_ButtonMouseState mouseState;
	ZF_ButtonLogicalState logicalState;

	int getStateInt() {
		if (disabled) { return 6; }
		else {
			return int(logicalState) * 3 + int(mouseState);
		}
	}

	void copyFrom(ZF_ButtonState from) {
		self.disabled = from.disabled;
		self.mouseState = from.mouseState;
		self.logicalState = from.logicalState;
	}

	bool eq(ZF_ButtonState other) {
		return
			self.disabled == other.disabled &&
			self.mouseState == other.mouseState &&
			self.logicalState == other.logicalState;
	}
}

class ZF_ButtonBase : ZF_ElementContainer {
	protected void makeButtonState(ZF_ButtonState s, bool logicalOn, bool hover, bool clicked, bool disabled) {
		s.mouseState =
			clicked ? ZF_ButtonMouseState_Clicked :
				(hover ? ZF_ButtonMouseState_Hover : ZF_ButtonMouseState_Nothing);
		s.logicalState = logicalOn ? ZF_ButtonLogicalState_On : ZF_ButtonLogicalState_Off;
		s.disabled = disabled;
	}

	protected Font fnt;
	Font getFont() { return self.fnt; }
	void setFont(Font fnt) {
		let old = self.fnt;
		if (fnt == NULL) {
			self.fnt = smallfont;
		}
		else {
			self.fnt = fnt;
		}
		let animator = ZF_ButtonAnimator(animator);
		if (shouldUpdateAnimator()) { animator.fontChange(self, old, self.fnt, animStorage); }
	}

	protected string text;
	string getText() { return self.text; }
	void setText(string text) {
		let old = self.text;
		let animator = ZF_ButtonAnimator(animator);
		self.text = text; if (shouldUpdateAnimator()) { animator.textChange(self, old, self.text, animStorage); }
	}

	protected int textColor;
	int getTextColor() { return self.textColor; }
	void setTextColor(int textColor) {
		let old = self.textColor;
		let animator = ZF_ButtonAnimator(animator);
		self.textColor = textColor; if (shouldUpdateAnimator()) { animator.textColorChange(self, old, self.textColor, animStorage); }
	}

	protected double textScale;
	double getTextScale() { return self.textScale; }
	void setTextScale(double textScale) {
		let old = self.textScale;
		let animator = ZF_ButtonAnimator(animator);
		self.textScale = textScale; if (shouldUpdateAnimator()) { animator.textScaleChange(self, old, self.textScale, animStorage); }
	}

	protected ZF_AlignType alignment;
	ZF_AlignType getAlignment() { return self.alignment; }
	void setAlignment(ZF_AlignType alignment) {
		let old = self.alignment;
		let animator = ZF_ButtonAnimator(animator);
		self.alignment = alignment; if (shouldUpdateAnimator()) { animator.alignmentChange(self, old, self.alignment, animStorage); }
	}
		
	protected ZF_ButtonState curButtonState;
	void getCurButtonState(ZF_ButtonState o) { o.copyFrom(self.curButtonState); }
	protected void setCurButtonState(ZF_ButtonState curButtonState) {
		ZF_ButtonState old; old.copyFrom(self.curButtonState);
		let animator = ZF_ButtonAnimator(animator);
		self.curButtonState.copyFrom(curButtonState);
		if (shouldUpdateAnimator()) { animator.stateChange(self, old, self.curButtonState, animStorage); }
	}
}
